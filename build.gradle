plugins {
	id "com.crystaelix.loom" version "1.13-SNAPSHOT"
	id "eclipse"
	id "idea"
	id "maven-publish"
	id "com.crystaelix.blossom" version "1.3.2"
	id "me.modmuss50.mod-publish-plugin" version "1.0.0"
}

repositories {
	// Add repositories to retrieve artifacts from, here
	mavenCentral()
	flatDir {
		dirs "libs"
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://maven.blamejared.com/"
			}
		}
		filter {
			includeGroup "mezz.jei"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://cursemaven.com/"
			}
		}
		filter {
			includeGroup "curse.maven"
		}
	}
}

version = mod_version
group = mod_group
def archivesName = "${mod_name}-1.16.5"
base.archivesName = archivesName

sourceSets {
	main
}

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(8)
		vendor = JvmVendorSpec.AZUL
	}
	withSourcesJar()
}

tasks.withType(JavaCompile).configureEach {
	options.encoding = "UTF-8"
}

loom {
	silentMojangMappingsLicense()
	forge {
		// Specify mixin json configs here
		mixinConfig "${mod_id}.mixins.json"
	}

	mixin.defaultRefmapName = "${mod_id}.refmap.json"
	runs {
		client {
			vmArg "-Xmx7G"
		}
		server {
			vmArg "-Xmx3G"
		}
	}
}

dependencies {
	// Minecraft dependencies, required
	minecraft "com.mojang:minecraft:1.16.5"
	mappings loom.layered {
		srg {
			targetNamespace "named"
		}
		officialMojangMappings {
			skipClassNames = true
		}
		parchment "org.parchmentmc.data:parchment-1.16.5:${parchment_version}@zip"
	}
	forge "net.minecraftforge:forge:${forge_version}"

	// Example dependencies
	modImplementation "mezz.jei:jei-1.16.5:${jei_version}"
}

def replacements = [
	"0@VERSION@": version
]

blossom {
	replaceToken(replacements)
}

processResources {
	inputs.properties(replacements)
	filesMatching(["mcmod.info", "META-INF/mods.toml", "META-INF/neoforge.mods.toml", "fabric.mod.json"]) {
		filter {
			it.replace(replacements)
		}
	}
}

def manifestAttributes = [
	"Specification-Title": mod_name,
	"Specification-Vendor": mod_author,
	"Specification-Version": mod_spec_version,
	"Implementation-Title": archivesName,
	"Implementation-Version": mod_version,
	"Implementation-Vendor": mod_author,
]

jar {
	manifest.attributes(manifestAttributes)
}

sourcesJar {
	manifest.attributes(manifestAttributes)
}

// Please refer to Mod Publish Plugin's documentation on how to use the plugin to upload projects to CurseForge/Modrinth
publishMods {
	file = remapJar.archiveFile
	version = displayName = "1.16.5-${mod_version}"
	changelog = file("changelog.md").text
	type = ALPHA
	modLoaders.add("forge")
	additionalFiles.from(sourcesJar.archiveFile)

	curseforge {
		projectId = curseforge_project_id
		accessToken = providers.gradleProperty("curseforge.token")
		minecraftVersions.add("1.16.5")
	}
	modrinth {
		projectId = modrinth_project_id
		accessToken = providers.gradleProperty("modrinth.token")
		minecraftVersions.add("1.16.5")
	}
}
